//===----------------------------------------------------------------------===//
// Copyright Â© 2025-2026 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
module pkl.impl.ghactions.jobs.HawkeyeCheck

extends "PklJob.pkl"

import "@com.github.actions/catalog.pkl"

// v6.4.1
image: String =
  "ghcr.io/korandoru/hawkeye@sha256:e0abf8dbfaeeeba1249679d662d9fb404b1dcdb041c6093ea1f7a6edb7d89319"

configPath: String = "licenserc.toml"

failIfUnknown: Boolean = true

failIfMissing: Boolean = false

fixed job {
  `runs-on` = "ubuntu-latest"
  container {
    image = module.image
  }
  name = "hawkeye-check"
  steps {
    (catalog.`actions/checkout@v6`) {
      with {
        // required to set git attrs
        `fetch-depth` = 0
        // used the PR HEAD if runnning on a PR
        ref =
          "${{ github.event_name == 'pull_request' && github.event.pull_request.head.sha || '' }}"
      }
    }
    // force git to behave in the container
    // the checkout action allegedly does this
    // but this has been required for hawkeye in some cases
    new { run = "git config --global --add safe.directory $(pwd)" }
    new {
      run =
        new Listing {
          "hawkeye check"
          "--config \(configPath)"
          when (failIfUnknown) {
            "--fail-if-unknown"
          }
          when (failIfMissing) {
            "--fail-if-missing true"
          }
        }.join(" ")
    }
  }
}
