//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
/// Checks that the Pkl definitions eval correctly, and that they produce no diffs in git.
module pkl.impl.ghactions.jobs.VerifyPklEval

import "@com.github.actions/catalog.pkl"
import "@com.github.actions/Workflow.pkl"

import "../steps/SetupPkl.pkl"

command: EvalCommand

class EvalCommand {
  modules: Listing<String>

  multipleFileOutputPath: String?

  outputPath: String(multipleFileOutputPath == null)?

  projectDir: String?

  fixed run: String =
    new Listing<String> {
      "pkl"
      "eval"
      when (multipleFileOutputPath != null) {
        "-m"
        multipleFileOutputPath
      }
      when (outputPath != null) {
        "-o"
        outputPath
      }
      when (projectDir != null) {
        "--project-dir"
        projectDir
      }
      ...modules
    }.join(" ")
}

fixed job: Workflow.Job = new {
  `runs-on` = "ubuntu-latest"
  steps {
    catalog.`actions/checkout@v6`
    new SetupPkl { version = "0.30.0" }.step
    new {
      shell = "bash"
      run = command.run
    }
    new {
      shell = "bash"
      name = "check git status"
      run =
        """
        if [ -n "$(git status --porcelain)" ]; then
          echo "Running pkl resulted in a diff! You likely need to run 'pkl eval' and commit the changes."
          git diff --name-only
          exit 1
        fi
        """
    }
  }
}
