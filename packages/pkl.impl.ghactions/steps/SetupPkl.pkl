//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
module pkl.impl.ghactions.steps.SetupPkl

import "@gha/Workflow.pkl"

version: String

platform: *"linux" | "alpine-linux" | "macos" | "windows"

arch: *"amd64" | "aarch64"

fixed fileExtension: String = if (platform == "windows") ".exe" else ""

fixed filename: String = "pkl-\(platform)-\(arch)\(fileExtension)"

fixed downloadUrl: String = "https://github.com/apple/pkl/releases/download/\(version)/\(filename)"

fixed step: Workflow.Step = new {
  id = "setup-pkl"
  shell = "bash"
  env {
    ["PKL_VERSION"] = version
    ["PKL_FILENAME"] = "pkl\(fileExtension)"
    ["PKL_DOWNLOAD_URL"] = downloadUrl
  }
  name = "Setup Pkl"
  // language=bash
  run =
    """
    set -ex

    DIR="$(mktemp -d /tmp/pkl-$PKL_VERSION-XXXXXX)"
    PKL_EXEC="$DIR/$PKL_FILENAME"
    curl -sfL -o $PKL_EXEC "$PKL_DOWNLOAD_URL"
    chmod +x $PKL_EXEC
    echo "$DIR" >> "$GITHUB_PATH"
    echo "pkl_exec=$PKL_EXEC" >> "$GITHUB_OUTPUT"
    """
}
