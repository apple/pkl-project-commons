//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
/// A template for writing Pkl-based workflows, whose actions are pinned to specific SHAs, and
/// updated by dependabot.
///
/// Typically, this module is placed in `.github/index.pkl`.
/// To produce output, the `-m` (or `--multiple-file-output-path`) flag must pointed at the repo's
/// `.github` directory.
// TODO move me to pkl-pantry
open module pkl.impl.ghactions.DependabotManagedCI

import "@com.github.actions/Workflow.pkl"
import "@com.github.dependabot/v2/Dependabot.pkl"

import "LockfileGenerator.pkl"

/// The effective workflows produced by this job, keyed by the workflow filename.
///
/// Example:
///
/// ```
/// workflows {
///   ["workflow/prb.yml"] = myPrbWorkflow
/// }
/// ```
workflows: Mapping<String, Workflow>

/// Dependabot configuration.
///
/// The `github-actions` ecosystem will be automatically added to this.
dependabot: Dependabot

local testMode = read?("prop:testMode") != null

local generator: LockfileGenerator = new {
  workflows = module.workflows.toMap().values
}

local locks = generator.locks

local effectiveDependabot = (dependabot) {
  updates {
    new {
      `package-ecosystem` = "github-actions"
      directory = "/"
      schedule {
        interval = "weekly"
      }
      ignore {
        new {
          `dependency-name` = "*"
          `update-types` {
            "version-update:semver-major"
          }
        }
      }
    }
  }
}

local convertedWorkflows: Mapping<String, Workflow> =
  // don't do any lockfile stuff if in test mode.
  if (testMode)
    workflows
  else
    (workflows) {
      [[true]] {
        output {
          renderer {
            converters {
              ["jobs[*].steps[*].uses"] = (it) -> locks.getOrNull(it)?.renderDirective
            }
          }
        }
      }
    }

output {
  text = throw("This module only supports multiple-file output. Evaluate me with the `-m` flag.")
  files = new {
    when (!testMode) {
      [generator.lockfileName] = generator.lockfileWorkflow.output
      ["dependabot.yml"] = effectiveDependabot.output
    }
    for (name, workflow in convertedWorkflows) {
      [name] = workflow.output
    }
  }
}
