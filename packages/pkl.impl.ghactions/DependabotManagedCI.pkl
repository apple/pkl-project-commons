//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
/// A template for writing Pkl-based workflows, whose actions are pinned to specific SHAs, and
/// updated by dependabot.
///
/// Typically, this module is placed in `.github/index.pkl`.
/// To produce output, the `-m` (or `--multiple-file-output-path`) flag must pointed at the repo's
/// `.github` directory.
///
/// ## Initialization
///
/// When first using this module, or whenever a new action is added, the lockfile and dependabot.yml
/// must be generated.
/// To generate these two files, pass `--prop mode=lockfile` during evaluation.
// TODO move me to pkl-pantry
open module pkl.impl.ghactions.DependabotManagedCI

import "@com.github.actions/AbstractTypedStep.pkl"
import "@com.github.actions/catalog.pkl"
import "@com.github.actions/Workflow.pkl"

import "DependabotV2.pkl"
import "LockfileGenerator.pkl"
import "steps/SetupPkl.pkl"

/// The effective workflows produced by this job, keyed by the workflow filename.
///
/// Example:
///
/// ```
/// workflows {
///   ["workflow/prb.yml"] = myPrbWorkflow
/// }
/// ```
workflows: Mapping<String, Workflow>

/// Dependabot configuration.
///
/// The github-actions ecosystem will be automatically added to this.
dependabot: DependabotV2

/// A map of action name to its checksum.
///
/// Parsed from `__lockfile__.yml`.
///
/// e.g. `Map("actions/checkout@v5", "actions/checkout@abc123")`
local locks: Map<String, LockfileGenerator.LockEntry> =
  generator.currentLocks ?? throw("Missing lockfile! Run with -p mode=lockfile to generate it.")

/// Two modes:
/// * `test`: don't replace versions with SHAs (internal development option)
/// * `lockfile`: instead of generating normal workflow files, generate `workflows/__lockfile__.yml`
///    and `dependabot.yml`.
local mode = read?("prop:mode") as ("test" | "lockfile")?

local generator: LockfileGenerator = new {
  workflows = module.workflows.toMap().values
}

local effectiveDependabot: DependabotV2 = (dependabot) {
  updates {
    new {
      `package-ecosystem` = "github-actions"
      directory = "/"
      schedule {
        interval = "weekly"
      }
      `exclude-paths` {
        for (name, _ in actualWorkflows) {
          ".github/\(name)"
        }
      }
    }
  }
}

// When dependabot submits a PR, add a commit that generates the rest of the workflows.
local dependabotPrPolish: Workflow = new {
  name = "dependabot-pr-polish"
  on {
    pull_request {}
  }
  jobs {
    ["update-pkl"] {
      `if` = "github.actor == 'dependabot[bot]'"
      `runs-on` = "ubuntu-latest"
      steps {
        (catalog.`actions/checkout@v6`) {
          with {
            `persist-credentials` = true
          }
        }
        new {
          name = "Check diff"
          id = "check-lockfile"
          // language=bash
          run =
            """
            if git diff HEAD~1 | grep -q .github/workflows/__lockfile__.yml; then
              echo "lockfile-changed=true" >> $GITHUB_OUTPUT
            else
              echo "lockfile-changed=false" >> $GITHUB_OUTPUT
            fi
            """
        }
        (new SetupPkl { version = "0.30.1" }.step) {
          `if` = "steps.check-lockfile.outputs.lockfile-changed == 'true'"
        }
        new {
          name = "Update generated workflows"
          `if` = "steps.check-lockfile.outputs.lockfile-changed == 'true'"
          run = "pkl eval --project-dir .github/ -m .github/ .github/index.pkl"
        }
        new {
          name = "Commit and push"
          `if` = "steps.check-lockfile.outputs.lockfile-changed == 'true'"
          // language=bash
          run =
            """
            git add .
            git commit -m "Update generated Pkl-based workflows"
            git push
            """
        }
      }
    }
  }
}

local actualWorkflows = (workflows) {
  ["workflows/__dependabot_pr_polish__.yml"] = dependabotPrPolish
}

local mappedWorkflows: Mapping<String, Workflow> =
  if (mode == "test")
    actualWorkflows
  else
    new {
      for (name, workflow in actualWorkflows) {
        [name] = (workflow) {
          jobs {
            [[true]] {
              steps = new {
                for (step in super.steps) {
                  step |> replaceVersionWithSha
                }
              }
            }
          }
        }
      }
    }

local replaceVersionWithSha = (step: Workflow.Step) ->
  if (step.uses == null)
    step
  else
    let (lockEntry = locks.getOrNull(step.uses))
      if (lockEntry == null)
        throw("The lockfile is out-dated and must be re-generated (using `-p mode=lockfile`)")
      else if (step is AbstractTypedStep)
        (step) { sha = lockEntry.sha }
      else
        (step) { uses = lockEntry.usesWithChecksum }

output {
  text = throw("This module only supports multiple-file output. Evaluate me with the `-m` flag.")
  files =
    if (mode == "lockfile")
      new {
        [generator.lockfileName] = generator.result.output
        ["dependabot.yml"] = effectiveDependabot.output
      }
    else
      new {
        for (name, workflow in mappedWorkflows) {
          [name] = workflow.output
        }
      }
}
