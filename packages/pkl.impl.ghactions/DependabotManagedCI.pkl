//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
/// A template for writing Pkl-based workflows, whose actions are pinned to specific SHAs, and
/// updated by dependabot.
///
/// Typically, this module is placed in `.github/index.pkl`.
/// To produce output, the `-m` (or `--multiple-file-output-path`) flag must pointed at the repo's
/// `.github` directory.
///
/// ## Initialization
///
/// When first using this module, or whenever a new action is added, the lockfile and dependabot.yml
/// must be generated.
/// To generate these two files, pass `--prop mode=lockfile` during evaluation.
// TODO move me to pkl-pantry
open module pkl.impl.ghactions.DependabotManagedCI

import "@com.github.actions/Workflow.pkl"

import "DependabotV2.pkl"
import "LockfileGenerator.pkl"
import "util.pkl"

/// The effective workflows produced by this job, keyed by the workflow filename.
///
/// Example:
///
/// ```
/// workflows {
///   ["workflow/prb.yml"] = myPrbWorkflow
/// }
/// ```
workflows: Mapping<String, Workflow>

/// Dependabot configuration.
///
/// The `github-actions` ecosystem will be automatically added to this.
dependabot: DependabotV2

/// Two modes:
/// * `test`: don't replace versions with SHAs (internal development option).
/// * `lockfile`: instead of generating normal workflow files, generate `workflows/__lockfile__.yml`
///    and `dependabot.yml`.
local mode = read?("prop:mode") as ("test" | "lockfile")?

local generator: LockfileGenerator = new {
  workflows = module.workflows.toMap().values
}

local locks =
  generator.currentLocks ?? throw("Missing lockfile! Run with -p mode=lockfile to generate it.")

local effectiveDependabot = (dependabot) {
  updates {
    new {
      `package-ecosystem` = "github-actions"
      directory = "/"
      schedule {
        interval = "weekly"
      }
    }
  }
}

local convertedWorkflows: Mapping<String, Workflow> =
  // don't do any lockfile stuff if in test mode.
  if (mode == "test")
    workflows
  else
    new {
      for (name, workflow in workflows) {
        [name] = (workflow) {
          output {
            text = super.text |> util.replaceUses(locks)
          }
        }
      }
    }

output {
  text = throw("This module only supports multiple-file output. Evaluate me with the `-m` flag.")
  files =
    if (mode == "lockfile")
      new {
        [generator.lockfileName] = generator.result.output
        ["dependabot.yml"] = effectiveDependabot.output
      }
    else
      new {
        for (name, workflow in convertedWorkflows) {
          [name] = workflow.output
        }
      }
}
