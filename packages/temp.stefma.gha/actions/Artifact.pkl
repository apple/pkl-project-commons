//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
/// Actions for managing artifacts in GitHub workflows.
/// This module is a low-level module not indended for direct use.
/// Use the [Action](../Action) module instead to access these actions.
module temp.stefma.gha.actions.Artifact

import "../Workflow.pkl"

/// Type-safe GitHub Action of the [download-artifact](https://github.com/actions/download-artifact) action.
class Download extends Workflow.Step {
  uses: "actions/download-artifact@v4"

  /// Name of the artifact to download.
  ///
  /// If unspecified, all artifacts for the run are downloaded.
  hidden artifactName: String?

  /// IDs of the artifacts to download, comma-separated.
  /// Either inputs `artifact-ids` or `name` can be used, but not both.
  /// Optional.
  hidden artifactIds: String(artifactName == null)?

  /// Destination path.
  ///
  /// Supports basic tilde expansion.
  /// Optional. Default is `$GITHUB_WORKSPACE`
  hidden path: String?

  /// A glob pattern to the artifacts that should be downloaded.
  /// Ignored if name is specified.
  /// Optional.
  hidden pattern: String?

  /// When multiple artifacts are matched, this changes the behavior of the destination directories.
  ///
  /// If true, the downloaded artifacts will be in the same directory specified by path.
  /// If false, the downloaded artifacts will be extracted into individual named directories within the specified path.
  /// Note: When downloading a single artifact (by name or ID), it will always be extracted directly to the specified path.
  /// Optional. Default is 'false'
  hidden mergeMultiple: Boolean?

  /// The GitHub token used to authenticate with the GitHub API.
  ///
  /// This is required when downloading artifacts from a different repository or from a different workflow run.
  ///
  /// Optional. If unspecified, the action will download artifacts from the current repo and the current workflow run.
  hidden githubToken: String?

  /// The repository owner and the repository name joined together by "/".
  ///
  /// If [githubToken] is specified, this is the repository that artifacts will be downloaded from.
  /// Optional. Default is `${{ github.repository }}`
  hidden repository: String?

  /// The id of the workflow run where the desired download artifact was uploaded from.
  ///
  /// If github-token is specified, this is the run that artifacts will be downloaded from.
  /// Optional. Default is ${{ github.run_id }}
  hidden runId: (String|Number|Boolean)?

  local localWith = new Mapping {
    ["name"] = artifactName
    ["artifact-ids"] = artifactIds
    ["path"] = path
    ["pattern"] = pattern
    ["merge-multiple"] = mergeMultiple
    ["github-token"] = githubToken
    ["repository"] = repository
    ["run-id"] = runId
  }
  local filteredWith = localWith.toMap().filter((_, value) -> value != null)
  with = if (filteredWith.isEmpty) null else filteredWith.toMapping()
}

/// Type-safe GitHub Action of the [upload-artifact](https://github.com/actions/upload-artifact) action.
class Upload extends Workflow.Step {
  uses: "actions/upload-artifact@v4"

  /// Name of the artifact to upload.
  ///
  /// Optional. Default is `'artifact'`
  hidden artifactName: String?

  /// A file, directory or wildcard pattern that describes what to upload
  hidden path: String

  /// The desired behavior if no files are found using the provided path.
  ///
  /// Available Options:
  ///
  ///   * `warn`: Output a warning but do not fail the action
  ///   * `error`: Fail the action with an error message
  ///   * `ignore`: Do not output any warnings or errors, the action does not fail
  ///
  /// Optional. Default is 'warn'
  hidden ifNoFilesFound: ("warn"|"error"|"ignore")?

  /// Duration after which artifact will expire in days.
  ///
  /// 0 means using default retention.
  /// Minimum 1 day.
  /// Maximum 90 days unless changed from the repository settings page.
  ///
  /// Optional. Defaults to repository settings.
  hidden retentionDays: Int?

  /// The level of compression for Zlib to be applied to the artifact archive.
  ///
  /// The value can range from 0 to 9.
  /// For large files that are not easily compressed, a value of 0 is recommended for significantly faster uploads.
  /// Optional. Default is '6'
  hidden compressionLevel: Int(isBetween(0, 9))?

  /// If `true`, an artifact with a matching name will be deleted before a new one is uploaded.
  /// If `false`, the action will fail if an artifact for the given name already exists.
  /// Does not fail if the artifact does not exist.
  ///
  /// Optional. Default is 'false'
  hidden overwrite: Boolean?

  /// Whether to include hidden files in the provided path in the artifact.
  ///
  /// The file contents of any hidden files in the path should be validated before
  /// enabled this to avoid uploading sensitive information.
  ///
  /// Optional. Default is 'false'
  hidden includeHiddenFiles: Boolean?

  local localWith = new Mapping {
    ["name"] = artifactName
    ["path"] = path
    ["if-no-files-found"] = ifNoFilesFound
    ["retention-days"] = retentionDays
    ["compression-level"] = compressionLevel
    ["overwrite"] = overwrite
    ["include-hidden-files"] = includeHiddenFiles
  }
  local filteredWith = localWith.toMap().filter((_, value) -> value != null)
  with = if (filteredWith.isEmpty) null else filteredWith.toMapping()
}

/// Type-safe GitHub Action of the [upload-artifact/merge](https://github.com/actions/upload-artifact/tree/main/merge) action.
class UploadArtifactMerge extends Workflow.Step {
  uses: "actions/upload-artifact/merge@v4"

  /// The name of the artifact that the artifacts will be merged into
  ///
  /// Optional. Default is 'merged-artifacts'
  hidden artifactName: String?

  /// A glob pattern matching the artifacts that should be merged.
  ///
  /// Optional. Default is '*'
  hidden pattern: String?

  /// If true, the artifacts will be merged into separate directories.
  /// If false, the artifacts will be merged into the root of the destination.
  ///
  /// Optional. Default is 'false'
  hidden separateDirectories: Boolean?

  /// Duration after which artifact will expire in days.
  ///
  /// 0 means using default retention.
  /// Minimum 1 day.
  /// Maximum 90 days unless changed from the repository settings page.
  ///
  /// Optional. Defaults to repository settings.
  hidden retentionDays: Int?

  /// The level of compression for Zlib to be applied to the artifact archive.
  ///
  /// The value can range from 0 to 9.
  /// For large files that are not easily compressed, a value of 0 is recommended for significantly faster uploads.
  /// Optional. Default is '6'
  hidden compressionLevel: Int(isBetween(0, 9))?

  /// If true, the artifacts that were merged will be deleted.
  /// If false, the artifacts will still exist.
  ///
  /// Optional. Default is `false`
  hidden deleteMerged: Boolean?

  /// If true, hidden files will be included in the merged artifact.
  /// If false, hidden files will be excluded from the merged artifact.
  ///
  /// Default is `false`
  hidden includeHiddenFiles: Boolean?

  local localWith = new Mapping {
    ["name"] = artifactName
    ["pattern"] = pattern
    ["separate-directories"] = separateDirectories
    ["retention-days"] = retentionDays
    ["compression-level"] = compressionLevel
    ["delete-merged"] = deleteMerged
    ["include-hidden-files"] = includeHiddenFiles
  }
  local filteredWith = localWith.toMap().filter((_, value) -> value != null)
  with = if (filteredWith.isEmpty) null else filteredWith.toMapping()
}
